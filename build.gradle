plugins {
    id 'java-library'
    id 'maven-publish'
}

// 自定义变量
ext {
    JAVA_VERSION = 11
    PACKAGE_NAME = 'com.ldzsai'
    LIB_VERSION = '0.0.1-SNAPSHOT'
}

java {
    withJavadocJar()
    withSourcesJar()
}

group = PACKAGE_NAME
version = LIB_VERSION
sourceCompatibility = JAVA_VERSION
targetCompatibility = JAVA_VERSION

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// 所有项目的配置
allprojects {

    sourceCompatibility = JAVA_VERSION
    targetCompatibility = JAVA_VERSION

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public'  }
        maven{ url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/release' }
        mavenCentral()
    }

    // 配置字符编码
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

// maven仓库相关
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = PACKAGE_NAME
            artifactId = 'kelp'
            version = LIB_VERSION
            from components.java

            pom {
                name = 'kelp'
                description = 'High performance EL expression parser'
                url = 'https://github.com/ldzsai/kelp'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ldzsai/kelp?tab=MIT-1-ov-file'
                    }
                }
                developers {
                    developer {
                        id = 'ldzsai'
                        name = 'ldzsai'
                        email = 'contact@ldzsai.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = uri("$buildDir/repository")
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.10.0'
}

tasks.named('test') {
    useJUnitPlatform()
}
